import org.apache.tools.ant.filters.*

jar.enabled = false

dependencies {
    testCompile project(':asciidoctorj')
    testCompile project(path: ':asciidoctorj-arquillian-extension')
    testCompile "commons-io:commons-io:$commonsioVersion"
    testCompile "org.jsoup:jsoup:$jsoupVersion"
    testCompile("org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion") {
        transitive = false
    }
}


task processAsciidocFiles(type: Copy) {
    def fromDir = 'src/main/asciidoc'

    inputs.files fileTree(dir: 'src')
    outputs.files fileTree(dir: rootProject.file('docs'), include: '*.adoc')

    dependsOn test
    from fromDir
    include '*.adoc'
    into rootProject.file('docs')
    eachFile { file ->
        file.filter { line ->

            switch (line) {
                case ~/^include::(.*?)\[(.*?)\]\s*$/:
                    String target = java.util.regex.Matcher.lastMatcher[0][1]
                    String attributes = java.util.regex.Matcher.lastMatcher[0][2]

                    String text = project.file(target).text
                    switch (attributes) {
                        case ~/lines=([0-9]+)\.\.((-1)|([0-9]+))/:
                            // line number based inclusion
                            int from = java.util.regex.Matcher.lastMatcher[0][1].toInteger()
                            int to = java.util.regex.Matcher.lastMatcher[0][2].toInteger()

                            List<String> lines = text.readLines()
                            if (to == -1) {
                                lines.subList(from - 1, lines.size()).join('\n')
                            } else {
                                lines.subList(from - 1, to).join('\n')
                            }
                            break;
                        case ~/tags=(.*)/:
                            // tag based inclusion
                            Set<String> inTags = [];
                            List<String> tags = java.util.regex.Matcher.lastMatcher[0][1].split(';') as List
                            List<String> taggedText = []

                            text.eachLine { textLine ->
                                switch (textLine) {
                                case ~/^.*tag::(.*?)\[(.*?)\]\s*$/:
                                    String tag = java.util.regex.Matcher.lastMatcher[0][1]
                                    if (tags.contains(tag)) {
                                        inTags << tag
                                    }
                                    break;
                                case ~/^.*end::(.*?)\[(.*?)\]\s*$/:
                                    inTags -= java.util.regex.Matcher.lastMatcher[0][1]
                                    break;
                                default:
                                    if (!inTags.empty) {
                                        taggedText << textLine
                                    }
                                    break
                                }
                            }
                            taggedText.join('\n')
                            break;
                        default:
                            text
                            break;
                    }
                    break
                default:
                    line
            }
        }
        // A Gradle issue makes calculating the source path a bit awkward: we cannot file.getFile() when using a a filter.
        // See: https://github.com/gradle/gradle/issues/1588
        def relativeSourceFile = file.getRelativeSourcePath().getFile(new File(fromDir))
        def absoluteSourceFile = new File("$projectDir", relativeSourceFile.toString())
        def rootProjectRelativeSourceFile = project.getRootProject().getProjectDir().toPath().
                relativize(absoluteSourceFile.toPath())
        def header = File.createTempFile("header",".ado")
        header.write """|// THIS FILE IS GENERATED - DO NOT EDIT
                        |// Edit here: ${rootProjectRelativeSourceFile}
                        |// And then run: ./gradlew asciidoctorj-documentation:build
                     """.stripMargin().trim().concat("\n")
        header.deleteOnExit()
        file.filter ConcatFilter, prepend : header
    }
}

build.dependsOn processAsciidocFiles

test.enabled = testClasses.enabled = JavaVersion.current().isJava7Compatible()

