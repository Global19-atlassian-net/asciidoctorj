apply plugin: 'application'

if (project.statusIsRelease) {
    apply from: rootProject.file('gradle/sdkman.gradle')
}

dependencies {
  compile(project(':asciidoctorj')) {
    exclude group: 'org.jruby'
  }

  compile(project(':asciidoctorj-api')) {
    transitive = false
  }

  compile("org.asciidoctor:asciidoctorj-epub3:$asciidoctorjEpub3Version") {
    transitive = false
  }
  compile("org.asciidoctor:asciidoctorj-diagram:$asciidoctorjDiagramVersion") {
    transitive = false
  }
  compile("org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion") {
    transitive = false
  }
  compile "org.jruby:jruby-complete:$jrubyVersion"
  testCompile project(':asciidoctorj-arquillian-extension')
  testCompile "org.apache.pdfbox:pdfbox:$pdfboxVersion"
}

jar.enabled = false
if ( !project.hasProperty('skip.signing') ) {
  signPom.enabled = false
}

/*
UGLY HACK to workaround gradle-bintray-plugin compatibility with Gradle 5+

https://github.com/asciidoctor/asciidoctorj/issues/861
https://github.com/bintray/gradle-bintray-plugin/issues/300

WARNING: since the Groovy Gradle API is modified, this breaks build isolation when sharing a common Gradle daemon instance

This works because gradle-bintray-plugin is using Groovy dynamic compilation hence it is affected by Groovy runtime meta-programming

Tested with: Gradle 5.6.3 / gradle-bintray-plugin 1.8.4

TODO: remove as soon as bintray/gradle-bintray-plugin#300 is fixed and integrated
*/
Signature.metaClass.getToSignArtifact = { ->
  return (delegate as Signature).source
}

configurations.archives.with {
  //We need to remove the (non-existing) jar artifact, otherwise signing will fail
  artifacts.removeAll{ it.type.equals("jar") }
}

tasks.withType(AbstractArchiveTask) {
  baseName rootProject.name
  classifier 'bin' // use classifier for identification in repository list
  eachFile { it.path = it.path.replaceFirst('-bin(?=/)', '') }
  //destinationDir = ...
}

startScripts {
  unixStartScriptGenerator.template = resources.text.fromFile('src/main/resources/customUnixStartScript.txt')

  applicationName = rootProject.name
  mainClassName = 'org.asciidoctor.jruby.cli.AsciidoctorInvoker'
  defaultJvmOpts = [
    '-client', '-Xmn128m', '-Xms256m', '-Xmx256m',
    '-Xverify:none', '-XX:+TieredCompilation', '-XX:TieredStopAtLevel=1', '-XX:+DisableExplicitGC', '-Djruby.compile.mode=OFF'
  ]
}
